data {
  D = readFasta(file="data/horses_isochronous_sequences.fasta");
  taxa = D.taxa();
  L = D.nchar();
}
model {
  // 300 species / 375 k years
  λ ~ LogNormal(meanlog=-0.22, sdlog=2);
  mu ~ LogNormal(meanlog=-2, sdlog=1);
  // mean 365 k years ago, HPD95 = [137 972] k years
  tmrca ~ LogNormal(meanlog=5.9, sdlog=0.5);
  ψ ~ BirthDeath(lambda=λ, mu=mu, taxa=taxa, rootAge=tmrca);
  TL = ψ.treeLength();
  rootAge = ψ.rootAge();

  π ~ Dirichlet(conc=[1.0,1.0,1.0,1.0]);
  rates ~ Dirichlet(conc=[1.0, 1.0, 1.0, 1.0, 1.0, 1.0]);
  Q=gtr(freq=π, rates=rates);

  α ~ LogNormal(meanlog=0.0, sdlog=2.0);
  r ~ DiscretizeGamma(shape=α, ncat=4, replicates=L);

//TODO overload
  //pInv ~ Beta(alpha=1.0, beta=1.0);
  //r ~ DiscretizeGammaI(shape=α, ncat=4, pinv=pInv, replicates=L);

  clock ~ LogNormal(meanlog=-16.9, sdlog=5.0);
  D ~ PhyloCTMC(tree=ψ, siteRates=r, Q=Q, mu=clock);
}
